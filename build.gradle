import org.gradle.internal.os.OperatingSystem

plugins {
	id "java"
	id "application"
	id "io.freefair.lombok" version "5.3.3.3"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

def mainClassFQN = "app.BasicRedWindow"
def projectName = "jin"

application {
	mainClass.set(mainClassFQN)
}

project.ext.lwjglVersion = "3.2.3"
switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		break
	case OperatingSystem.WINDOWS:
		project.ext.lwjglNatives = "natives-windows"
		break
}
project.ext.jomlVersion = "1.9.25"

repositories {
	mavenCentral()
}

dependencies {
	implementation "com.google.guava:guava:29.0-jre"
	testImplementation "junit:junit:4.13"

	implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")
	implementation "org.lwjgl:lwjgl"
	implementation "org.lwjgl:lwjgl-assimp"
	implementation "org.lwjgl:lwjgl-glfw"
	implementation "org.lwjgl:lwjgl-openal"
	implementation "org.lwjgl:lwjgl-opengl"
	implementation "org.lwjgl:lwjgl-stb"
	runtimeOnly "org.lwjgl:lwjgl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-assimp::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-glfw::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-openal::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-opengl::$lwjglNatives"
	runtimeOnly "org.lwjgl:lwjgl-stb::$lwjglNatives"

	implementation "org.joml:joml:${jomlVersion}"

	implementation "net.sf.trove4j:trove4j:3.0.3"

	compileOnly "org.projectlombok:lombok"
}

task fatJar(type: Jar) {
	manifest {
		attributes "Main-Class": mainClassFQN
		attributes "Description": projectName
	}
	archiveBaseName = projectName
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
	from {
		configurations.runtimeClasspath
		.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	with jar

	exclude(
		"META-INF/*.DSA",
		"META-INF/*.SF",
		"META-INF/*.RSA"
	)
	with jar
}

compileJava {
	options.warnings = true
	options.deprecation = true
	options.compilerArgs += ["-Xlint:all"]
}
